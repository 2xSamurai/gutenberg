pluginManagement {
  plugins {
    id 'com.jfrog.bintray' version '1.8.5'
  }
}

rootProject.name = '@wordpress_react-native-bridge'

include ':react-native-bridge'
include ':@wordpress_react-native-aztec'
project(':@wordpress_react-native-aztec').projectDir = new File(rootProject.projectDir, '../../react-native-aztec/android')

def publishReactNativeBridgeVersion = properties["publishReactNativeBridgeVersion"]
gradle.ext.willPublishReactNativeBridgeBinary = publishReactNativeBridgeVersion?.trim() as boolean

// Only add these modules/projects if we are not building to publish a binary.
// TODO: This is a very fragile check. Since the `settings.gradle` file is part of initialization,
// it'll not work for even simple tasks such as `./gradlew clean` unless the
// `publishReactNativeBridgeVersion` property is passed. It's a necessary check for the moment,
// however by moving the Hermes binary to remote and maybe using dependency substitution for the
// included projects, we can get rid of it.
if (!gradle.ext.willPublishReactNativeBridgeBinary) {
    /*
    IMPORTANT: DO NOT add hermes modules if we are building a binary to publish.
    If these modules are added, it'll cause the `com.facebook.hermes.BuildConfig` file to be
    included in the binary which will then result in a duplicate class error.
    This seems to happen even if these modules are not added as a dependency.
    */
    include ':hermes-debug'
    include ':hermes-release'

    assert file("../../../node_modules").exists() : "node_modules folder is missing. Please run `npm install` from the root Gutenberg project"

    include ':react-native-linear-gradient'
    project(':react-native-linear-gradient').projectDir = new File(rootProject.projectDir, '../../../node_modules/react-native-linear-gradient/android')
    include ':react-native-svg'
    project(':react-native-svg').projectDir = new File(rootProject.projectDir, '../../../node_modules/react-native-svg/android')
    include ':react-native-aztec'
    project(':react-native-aztec').projectDir = new File(rootProject.projectDir, '../../../packages/react-native-aztec/android')
    include ':react-native-video'
    project(':react-native-video').projectDir = new File(rootProject.projectDir, '../../../node_modules/react-native-video/android-exoplayer')
    include ':@react-native-community_slider'
    project(':@react-native-community_slider').projectDir = new File(rootProject.projectDir, '../../../node_modules/@react-native-community/slider/src/android')
    include ':react-native-get-random-values'
    project(':react-native-get-random-values').projectDir = new File(rootProject.projectDir, '../../../node_modules/react-native-get-random-values/android')
    include ':@react-native-community_masked-view'
    project(':@react-native-community_masked-view').projectDir = new File(rootProject.projectDir, '../../../node_modules/@react-native-community/masked-view/android')
    include ':react-native-gesture-handler'
    project(':react-native-gesture-handler').projectDir = new File(rootProject.projectDir, '../../../node_modules/react-native-gesture-handler/android')
    include ':react-native-screens'
    project(':react-native-screens').projectDir = new File(rootProject.projectDir, '../../../node_modules/react-native-screens/android')
    include ':react-native-safe-area-context'
    project(':react-native-safe-area-context').projectDir = new File(rootProject.projectDir, '../../../node_modules/react-native-safe-area-context/android')
    include ':react-native-reanimated'
    project(':react-native-reanimated').projectDir = new File(rootProject.projectDir, '../../../node_modules/react-native-reanimated/android')
}

